plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.github.dcendents.android-maven'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.3'
    id "net.ltgt.errorprone" version "0.0.14"
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 5
        versionName "2.0.3"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

group = 'com.github.MatteoBattilana'
version = android.defaultConfig.versionName
project.archivesBaseName = 'WeatherView'

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Support lib
    implementation "com.android.support:support-compat:$support_library_version"
    implementation "com.android.support:support-annotations:$support_library_version"

    implementation 'com.github.plattysoft:Leonids:1746429'
    implementation 'com.github.jinatonic.confetti:confetti:1.1.1'

    testImplementation 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
